plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
    id 'maven-publish'
    id 'signing'
    id 'com.gradleup.nmcp' version '0.0.9'
}

version = project.mod_version
group = "io.github.pixelstudios-dev"

base {
    archivesName = project.archives_base_name
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand(
                "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
        )
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java

            pom {
                name = project.archives_base_name
                description = "A Fabric library made by PixelStudios"
                url = "https://github.com/pixelstudios-dev/${project.archives_base_name}"

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = "pixelstudios"
                        name = "PixelStudios Dev Team"
                        url = "https://github.com/pixelstudios-dev"
                    }
                }

                scm {
                    url = "https://github.com/pixelstudios-dev/${project.archives_base_name}"
                    connection = "scm:git:git://github.com/pixelstudios-dev/${project.archives_base_name}.git"
                    developerConnection = "scm:git:ssh://github.com:pixelstudios-dev/${project.archives_base_name}.git"
                }
            }
        }
    }
}

signing {
    def keyId = System.getenv("SIGNING_KEY_ID")
    def key = System.getenv("SIGNING_KEY")
    def password = System.getenv("SIGNING_PASSWORD")

    if (keyId && key && password) {
        useInMemoryPgpKeys(keyId, key, password)
        sign publishing.publications.mavenJava
    } else {
        println "Firma PGP desactivada (faltan claves)"
    }
}

nmcp {

    publishAllPublications {
        username = System.getenv("SONATYPE_USERNAME") ?: "token"
        password = System.getenv("SONATYPE_TOKEN")
        publicationType = "AUTOMATIC"
    }

}